; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Easy-IP Delphi Unit Dependency Scanner"
#define MyAppVerName "Easy-IP Delphi Unit Dependency Scanner V"
#define MyAppPublisher "Easy-IP AS"
#define MyAppURL "http://www.easy-ip.net/"
#Define AppVersion GetFileVersion("..\Bin\Duds\Win32\Release\DependencyScanner.exe")

#define BinDir "..\Bin\Duds\Win32\Release"
//#define CommonInstallDir = "..\Common\Install"
#define Year = GetDateTimeString('YYYY', '', '')

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppID={{C6C52C46-A2AC-46AD-9724-97E9F71E39EC}
AppName={#MyAppName}
AppVersion=V{#AppVersion}
;AppVerName={#MyAppName} V{#AppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={pf}\Unit Dependency Scanner
DefaultGroupName=Unit Dependency Scanner
OutputDir=..\..\..\Bin\Installers
OutputBaseFilename=EasyIPUnitDependencyScannerSetup
Compression=lzma2/Ultra64
SolidCompression=true
AppCopyright=Copyright © Easy-IP AS 2002-{#Year}
;LicenseFile=Text Files\EULA.rtf
Uninstallable=true
UninstallDisplayName={#MyAppName}
UninstallDisplayIcon={app}\DependencyScanner.exe
VersionInfoVersion={#AppVersion}
InternalCompressLevel=Ultra64
; If this is not commented out, you must compile via FinalBuilder
;SignTool=MySignTool
MinVersion=0,5.01

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Files]
Source: "{#BinDir}\DependencyScanner.exe"; DestDir: "{app}"
Source: ..\Docs\Version History.txt; DestDir: {app};

[Icons]
Name: {group}\Delphi Unit Dependency Scanner; Filename: {app}\DependencyScanner.exe; IconIndex: 0; Comment: Delphi Unit Dependency Scanner; WorkingDir: {userdocs}\Delphi Unit Dependency Scanner
Name: {group}\{cm:UninstallProgram,{#MyAppName}}; Filename: {uninstallexe}; 

[Run]
Filename: "{app}\DependencyScanner.exe"; Flags: nowait postinstall; Description: "Run Delphi Unit Dependency Scanner"

[Code]
#Include "FileUtils.iss"

procedure KillTasks;
begin
  KillTask('DependencyScanner.exe');
end;

procedure CurStepChanged(CurStep: TSetupStep);
var
  ErrorCode: Integer;
begin
  case CurStep of
    ssInstall:
      begin
        KillTasks;
      end;
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  case CurUninstallStep of
    usPostUninstall:
    begin
      KillTasks;
    end;
  end; // case
end;

function NextButtonClick(CurPage: Integer): Boolean;
begin
  Result := true;

  if CurPage = wpReady then
  begin
    KillTasks;
  end;
end;
